label meeting:

    # Music.
    play music "track 07 - noise.ogg"
    
    scene black
    nvl show dissolve

    nvl_narrator "I fell in love with Amélie at the first sight of her."

    play music "track 02 - bar.ogg"

    show bg bar with dissolve
    
    nvl_narrator " We met at a queer bar.{w} She was working behind the counter, she wore a harness over her shirt:{w} her countenance was sensual, sophisticated, and she moved with ease as she prepared cocktail after cocktail."

    show sub blouse harnais_blouse collier at right with dissolve

    nvl_narrator "I had come there to be part of a discussion group for queer women:{w} I meant to meet people and find friends, to talk about love, relationships and sex in a place where I’d be safe.{w} I had arrived too early, but the bar upstairs was already full of people who stood around, laughed, and talked together."

    show dom outside panic worry scared at left with dissolve
    nvl clear

    nvl_narrator "I hadn’t been in Paris for long, at the time."
    nvl_narrator "It was one of my first nights outside, and I wasn’t yet used to this type of setting :{w} the pink lights, the music, the alcohol, the people almost overwhelmed me, so much so that I considered going home for the night;{w} I stood near the bar, uncertain of what I should do."
    nvl_narrator "She noticed me and called out."

    nvl hide dissolve
    nvl clear

    sub cat seductive "Hey! Are you okay? Can I get you anything?"
    dom neutral "Oh! Sorry, I got lost.{w} And no, thank you, I don’t drink."
    sub seducing "As you wish! But do know we have softs too, and I can whip you up something."
    dom simplesmile "Thank you."
    sub "I don’t think I ever saw you here before, and I’m pretty sure I would remember someone even half as cute- "
    show sub grimace
    show dom joy
    "She held her tongue, feeling as though she had crossed a line and caught herself too late.{w} As for myself, I felt flattered, if a bit embarrassed."
    sub bigsmile cat "Sorry, sorry, I could not resist. But, seriously, tell me if I’m being too much.{w} We’re a nice place, and I want you to have a good time."
    dom "It’s fine. I’m just here for the meeting.{w} Can I have something sweet?"
    sub "Sure!"

    nvl show dissolve

    nvl_narrator """
    We talked for a bit, about our tastes, the various softs she could make, and then about the event.{w} I explained that I was from Provence, that I had come to this city for work, and that I was eager to meet friends in the local queer scene.

    Amélie listened well.{w} Occasionally, she had to prepare someone’s order, but she made sure to come back to me afterward and resume our conversation.

    Her smile, her attention felt earnest.{w} Soothing. She answered my questions about the group downstairs, she reassured me that everything would be fine, that I could trust the organiser and that she would be there for me if I needed any help.
    """

    nvl hide dissolve
    nvl clear

    "When time came to go downstairs, I was at ease, if a little saddened to leave."
    sub "Don’t hesitate to come back."
    sub "You seem really sweet and, if you’d like, I’d love to be your friend."

    # Tanit: quelle scène ici? rue? continuer bar?

    stop music fadeout 1.0

    play music "track 07 - noise.ogg"

    scene bg rue
    nvl show dissolve

    nvl_narrator """
      We met again afterwards, first at her bar, later at an association, and then as friends.{w} I got the opportunity to better know her. To see how cheerful and outgoing she was, even when she wasn’t behind a counter.

      She always had a pun at the tip of her tongue.

      She was so pretty, too.
    """
    scene bg restaurant
    
    nvl clear

    nvl_narrator """
    And her boldness!{w} She always said what was on her mind,{w} no matter who could hear her,{w} no matter the subject:{w} it was a sight to behold, that time she nerded out on the minutiae of vore and cannibalism in the middle of a fast food restaurant,{w} without any concern for the increasingly agitated looks of that couple on the table over.{w}

    Oh, one of our friends was beyond mortified, and when they later messaged Amélie to tell her about it, I thought that she would never stop apologising.{w} It was as though she hadn’t really thought that the topic was anything to be ashamed of or keep behind closed doors.
    
    It didn’t take long for me to realise that I had fallen for her.
    """

    nvl hide dissolve
    nvl clear
